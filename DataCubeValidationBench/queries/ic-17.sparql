prefix qb: <http://purl.org/linked-data/cube#> 
select distinct ?obs1 ?numMeasures (COUNT(?obs2) AS ?count) 
where { 
  { 
    SELECT ?dsd (COUNT(?m) AS ?numMeasures) WHERE { 
      ?dsd qb:component ?cs0 . 
      ?cs0 qb:componentProperty ?m . 
      ?m a qb:MeasureProperty . 
    } GROUP BY ?dsd 
  } 
  ?obs1 qb:dataSet ?dataset . 
  ?dataset qb:structure ?dsd . 
  ?obs1 qb:measureType ?m1 . 
  ?obs2 qb:dataSet ?dataset . 
  ?obs2 qb:measureType ?m2 . 
  FILTER NOT EXISTS { 
    ?dsd qb:component ?cs1 . 
    ?cs1 qb:componentProperty ?dim . 
    FILTER (?dim != qb:measureType) 
    ?dim a qb:DimensionProperty . 
    ?obs1 ?dim ?v1 . 
    ?obs2 ?dim ?v2 . 
    FILTER (?v1 != ?v2) 
  } 
} GROUP BY ?obs1 ?numMeasures 
